/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014 Sebastian RODRIGUEZ, Nicolas GAUD, StÃ©phane GALLAND.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
grammar io.sarl.lang.SARL with org.eclipse.xtend.core.Xtend

generate sarl "http://www.sarl.io/lang/SARL"

import "http://www.eclipse.org/xtend" as xtend
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvm
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/xtext/xbase/Xtype" as xtype

SarlScript:
	('package' name=QualifiedName ';'?)?
	importSection=XImportSection?
	elements+=Type*
;

VarArgToken:
	'*'
;

Type returns xtend::XtendTypeDeclaration:
	SarlEvent | SarlCapacity | SarlAgent | SarlBehavior | SarlSkill | SarlClass | SarlInterface | SarlEnum | SarlAnnotationType 
;

Member returns xtend::XtendMember:
	SarlField | SarlConstructor | SarlAction | SarlClass | SarlInterface | SarlEnum | SarlAnnotationType 
;

SarlField returns xtend::XtendMember:
	{xtend::XtendField}
	annotations+=XAnnotation*
	{xtend::XtendField.annotationInfo = current} modifiers+=CommonModifier*
	(
		modifiers+=FieldModifier modifiers+=CommonModifier* 
	|
		modifiers+='extension' (modifiers+=FieldModifier | modifiers+=CommonModifier)* 
	| 
		modifiers+=FieldModifier (modifiers+=CommonModifier)* modifiers+='extension' (modifiers+=CommonModifier)*  
	)
    (=>(name=ValidID ':' type=JvmTypeReference) | name=ValidID) 
	('=' initialValue=XExpression)? ';'?
;

SarlActionSignature returns xtend::XtendMember:
	{xtend::XtendFunction}
	annotations+=XAnnotation*
	{SarlActionSignature.annotationInfo = current} modifiers+=CommonModifier*
	modifiers+=MethodModifier (modifiers+=CommonModifier | modifiers+=MethodModifier)* 
	('<' typeParameters+=JvmTypeParameter (',' typeParameters+=JvmTypeParameter)* '>')?
	name=FunctionID
	(
		=> '('
		(	parameters+=Parameter
			(=>',' parameters+=Parameter)* 
			(varargs?=VarArgToken)?
		)?
		')'
	)?
	(	':'
		=>(returnType=JvmTypeReference createExtensionInfo=CreateExtensionInfo)
	  | =>(returnType=TypeReferenceWithTypeArgs)
	  | =>(returnType=TypeReferenceNoTypeArgs)
	  | =>(createExtensionInfo=CreateExtensionInfo)
	)?
	(	('throws' exceptions+=JvmTypeReference (',' exceptions+=JvmTypeReference)*)?
		&
		('fires' firedEvents+=JvmTypeReference (',' firedEvents+=JvmTypeReference)*)?
	) ';'?
;

SarlAction returns xtend::XtendMember:
	{xtend::XtendFunction}
	annotations+=XAnnotation*
	{SarlAction.annotationInfo = current} modifiers+=CommonModifier*
	modifiers+=MethodModifier (modifiers+=CommonModifier | modifiers+=MethodModifier)* 
	('<' typeParameters+=JvmTypeParameter (',' typeParameters+=JvmTypeParameter)* '>')?
	name=FunctionID
	(
		=> '('
		(	parameters+=Parameter
			(=>',' parameters+=Parameter)* 
			(varargs?=VarArgToken)?
		)?
		')'
	)?
	(	':'
		=>(returnType=JvmTypeReference createExtensionInfo=CreateExtensionInfo)
	  | =>(returnType=TypeReferenceWithTypeArgs)
	  | =>(returnType=TypeReferenceNoTypeArgs)
	  | =>(createExtensionInfo=CreateExtensionInfo)
	)?
	(	('throws' exceptions+=JvmTypeReference (',' exceptions+=JvmTypeReference)*)?
		&
		('fires' firedEvents+=JvmTypeReference (',' firedEvents+=JvmTypeReference)*)?
	)
	(expression=XBlockExpression | expression=RichString | ';')
;

SarlBehaviorUnit returns xtend::XtendMember:
	{SarlBehaviorUnit}
	annotations+=XAnnotation*
	{SarlBehaviorUnit.annotationInfo = current} 
	'on' name=JvmParameterizedTypeReference (=> '[' guard=XExpression ']')?
	(body=XBlockExpression | body=RichString | ';')
;

SarlCapacityUses returns xtend::XtendMember:
	{SarlCapacityUses}
	'uses' capacitiesUsed+=JvmParameterizedTypeReference
	(',' capacitiesUsed+=JvmParameterizedTypeReference)* ';'?
;

SarlRequiredCapacity returns xtend::XtendMember:
	{SarlRequiredCapacity}
	'requires' requiredCapacities+=JvmParameterizedTypeReference
	(',' requiredCapacities+=JvmParameterizedTypeReference)* ';'?
;

SarlConstructor returns xtend::XtendMember:
	{xtend::XtendConstructor}
	annotations+=XAnnotation*
	{SarlConstructor.annotationInfo = current}
	modifiers+=CommonModifier*
	'new'
	('<' typeParameters+=JvmTypeParameter (',' typeParameters+=JvmTypeParameter)* '>')?
	( '(' (parameters+=Parameter (',' parameters+=Parameter)*)?
		(varargs?=VarArgToken)?
	  ')'
	)?
	('throws' exceptions+=JvmTypeReference (',' exceptions+=JvmTypeReference)*)?
	expression=XBlockExpression
;

SarlClass returns xtend::XtendTypeDeclaration:
	{xtend::XtendClass}
	annotations+=XAnnotation*
	{xtend::XtendClass.annotationInfo = current}
	modifiers+=CommonModifier*
	'class' name=ValidID ('<' typeParameters+=JvmTypeParameter (',' typeParameters+=JvmTypeParameter)* '>')?  
	("extends" extends=JvmParameterizedTypeReference)? 
   	('implements' implements+=JvmParameterizedTypeReference (',' implements+=JvmParameterizedTypeReference)*)?'{'
	   (members+=Member)*
	'}'
;

SarlInterface returns xtend::XtendTypeDeclaration:
	{xtend::XtendInterface}
	annotations+=XAnnotation*
	{xtend::XtendInterface.annotationInfo = current}
	modifiers+=CommonModifier*
  	'interface' name=ValidID ('<' typeParameters+=JvmTypeParameter (',' typeParameters+=JvmTypeParameter)* '>')?  
   	('extends' extends+=JvmParameterizedTypeReference (',' extends+=JvmParameterizedTypeReference)*)?'{'
	   (members+=Member)*
   	'}'
;

SarlEnum returns xtend::XtendTypeDeclaration:
	{xtend::XtendEnum}
	annotations+=XAnnotation*
	{xtend::XtendEnum.annotationInfo = current}
	modifiers+=CommonModifier*
  	'enum' name=ValidID '{'   
	   (members+=XtendEnumLiteral (',' members+=XtendEnumLiteral)*)? ';'?
   	'}'
;

SarlAnnotationType returns xtend::XtendTypeDeclaration:
	{xtend::XtendAnnotationType}
	annotations+=XAnnotation*
 	{xtend::XtendAnnotationType.annotationInfo = current}
 	modifiers+=CommonModifier*
	'annotation' name=ValidID
	'{' (members+=AnnotationField)* '}'
;

SarlEvent returns xtend::XtendTypeDeclaration:
	{SarlEvent}
	annotations+=XAnnotation*
	{SarlEvent.annotationInfo = current}
	modifiers+=CommonModifier*
	'event' name=ValidID ('extends' superTypes+=JvmParameterizedTypeReference)? 
	('{' (features+=EventMember)* '}')?
;

EventMember returns xtend::XtendMember:
	SarlField | SarlConstructor
;

SarlAgent returns xtend::XtendTypeDeclaration:
	{SarlAgent}
	annotations+=XAnnotation*
	{SarlAgent.annotationInfo = current}
	modifiers+=CommonModifier*
	'agent' name=ValidID 
	('extends' superTypes+=JvmParameterizedTypeReference)?
	'{' (features+=AgentMember)* '}'
;

AgentMember returns xtend::XtendMember:
	Member | SarlBehaviorUnit | SarlCapacityUses | SarlRequiredCapacity
;

SarlCapacity returns xtend::XtendTypeDeclaration:
	{SarlCapacity}
	annotations+=XAnnotation*
	{SarlCapacity.annotationInfo = current}
	modifiers+=CommonModifier*
	'capacity' name=ValidID
	('extends' superTypes+=JvmParameterizedTypeReference
		( ',' superTypes+=JvmParameterizedTypeReference )* )? 
	'{' (features+=CapacityMember)* '}'
;

CapacityMember returns xtend::XtendMember:
	SarlActionSignature
;

SarlBehavior returns xtend::XtendTypeDeclaration:
	{SarlBehavior}
	annotations+=XAnnotation*
	{SarlBehavior.annotationInfo = current}
	modifiers+=CommonModifier*
	'behavior' name=ValidID ('extends' superTypes+=JvmParameterizedTypeReference)?
	'{' (features+=BehaviorMember)* '}'
;

BehaviorMember returns xtend::XtendMember:
	Member | SarlBehaviorUnit | SarlCapacityUses | SarlRequiredCapacity
;

SarlSkill returns xtend::XtendTypeDeclaration:
	{SarlSkill}
	annotations+=XAnnotation*
	{SarlSkill.annotationInfo = current}
	modifiers+=CommonModifier*
	'skill' name=ValidID 
	(	('extends' superTypes+=JvmParameterizedTypeReference)?
		&
		(	'implements' 
				(	implementedTypes+=JvmParameterizedTypeReference
					(',' implementedTypes+=JvmParameterizedTypeReference)*
				)
		)?
	)
	'{' (features+=SkillMember)* '}'
;

SkillMember returns xtend::XtendMember:
	Member | SarlBehaviorUnit | SarlCapacityUses | SarlRequiredCapacity
;

Parameter returns xtend::XtendParameter:
	{SarlFormalParameter}
	annotations+=XAnnotation*
	(extension?='extension' annotations+=XAnnotation*)?  
	name=InnerVarID ':' parameterType=JvmTypeReference 
	('=' defaultValue=DefaultParameterValue)?
;

DefaultParameterValue returns xbase::XExpression:
	XLiteral
;

//-----------------------------------------------
// BELOW THIS POINT, THE RULES FROM XBASE ARE OVERLOADED

// Variable declaration according to the SARL syntax (not the Xtext/Xtend)
XVariableDeclaration returns xbase::XExpression:
	=>({xtend::XtendVariableDeclaration}
	(((writeable?='var'|'val') extension?='extension'?) | (extension?='extension' (writeable?='var'|'val'))))
	(=>name=InnerVarID)
	(':' type=JvmTypeReference)?
	('=' right=XExpression)?
;

// Formal parameter declaration according to the SARL syntax (not the Xtext/Xtend)
JvmFormalParameter returns xtend::XtendFormalParameter:
	extension?='extension'? name=InnerVarID (=> ':' (parameterType=JvmTypeReference))?
;
	
// Formal parameter declaration according to the SARL syntax (not the Xtext/Xtend)
FullJvmFormalParameter returns xtend::XtendFormalParameter:
	extension?='extension'? name=InnerVarID ':' parameterType=JvmTypeReference
;

// The type of the for-loop's variable is following the SARL syntax (not the Xtext/Xtend)
XForLoopExpression returns xbase::XExpression:
	=>({xbase::XForLoopExpression}
	'for' '(' declaredParam=SarlXLoopFormalParameter ':') forExpression=XExpression ')'
	( eachExpression=XExpression | ';' )
;
// The following rule is not provided by Xbase, but we use it
// in the overloaded version of XForLoopExpression
SarlXLoopFormalParameter returns jvm::JvmFormalParameter:
	{jvm::JvmFormalParameter}
	name=ValidID
	( 'as' parameterType=JvmTypeReference )?
;

// The type of the switch's variable is following the SARL syntax (not the Xtext/Xtend)
XSwitchExpression returns xbase::XExpression:
	{xbase::XSwitchExpression}
	'switch' (=>('(' declaredParam=JvmFormalParameter '=') switch=XExpression ')'
		| =>(declaredParam=JvmFormalParameter '=')? switch=XExpressionOrSimpleConstructorCall) '{'
	(cases+=XCasePart)*
	('default' ':' default=XExpression )?
	'}'
;
